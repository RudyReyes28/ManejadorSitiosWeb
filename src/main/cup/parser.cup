package com.rudyreyes.manejadorweb;
import java.util.ArrayList;
import java.util.List;
import java_cup.runtime.*;
import com.rudyreyes.manejadorweb.modelo.componente.*;
import com.rudyreyes.manejadorweb.modelo.paginaweb.*;
import com.rudyreyes.manejadorweb.modelo.sitioweb.*;
class ParserXML;

parser code {:
    Componente nuevoComponente = new Componente();
    EliminarComponente elimComponente = new EliminarComponente();
    PaginaWeb nuevaPagina = new PaginaWeb();
    SitioWeb nuevoSitio = new SitioWeb();
    List<Object> listaAcciones = new ArrayList<>();


    public List<Object> obtenerAcciones(){
        return listaAcciones;
    }
    
:}


/*DEFINIENDO TERMINALES*/
terminal MENOR, MAYOR, DIAGONAL, ACCION, NOMBRE, IGUAL, COMILLA, CREARSITIO, BORRARSITIO,NUEVAPAGINA, PARAMETROS, PARAMETRO;
terminal BORRARPAGINA, IDPARAM, USUARIOCREACION, FECHAMODIFICACION, USUARIOMODIFICACION, FECHACREACION, PARAMETROSCIERRE;
terminal MODIFICARPAGINA, PAGINA, CLASE, ATRIBUTOS, ATRIBUTO,ATRIBUTOSCIERRE, TEXTO, COLOR,ORIGEN,  ALTURA, ANCHO, PADRE;
terminal ETIQUETAS, TITULO, SITIO, ETIQUETA, CONTENIDOETIQUETA, VALORETIQUETA, ETIQUETASCIERRE, TITULOC;
terminal PARRAFO, IMAGEN, VIDEO, MENU, AGREGARCOMPONENTE, ALINEACION, ELIMINARCOMPONENTE, MODIFICARCOMPONENTE;
terminal String LITERAL, VALORID, VALORFECHA, DATOETIQUETA;


/*NO TERMINALES*/
non terminal accion, tipo_accion, parametros_crear_sitio, parametros_borrar_sitio, parametros_borrar_pagina;
non terminal  parametro_lista_crear_sitio, parametro_borrar_sitio, parametros_nueva_pagina;
non terminal parametro_borrar_pagina, parametros_modificar_pagina, parametro_modificar_pagina;
non terminal parametro_lista_modificar_pagina, tipo_param_modificar_pagina, parametros_agregar_componente ;
non terminal parametro_lista_agregar_co, parametro_agregar_co, atributos, atributo_lista;
non terminal atributo, tipo_param_agregar_co, parametros_eliminar_componente;
non terminal parametro_lista_eliminar_co, parametro_eliminar_co, tipo_param_eliminar_co;
non terminal parametros_modificar_componente;
non terminal parametro_lista_nueva_pagina, parametro_nueva_pagina, tipo_param_nueva_pagina, etiquetas, etiqueta;
non terminal parametro_crear_sitio, tipo_param_crear_sitio, empty;
non terminal String valor_atributo, parametro_tipo_id, tipo_clase, parametro_tipo_usuario_creacion;
non terminal String parametro_tipo_fecha_creacion, parametro_tipo_fecha_mod, parametro_tipo_usuario_mod;
start with accion;

accion ::= MENOR ACCION NOMBRE IGUAL  COMILLA tipo_accion 
	|accion MENOR ACCION NOMBRE IGUAL  COMILLA tipo_accion
        ;
			
tipo_accion ::= CREARSITIO COMILLA MAYOR parametros_crear_sitio MENOR DIAGONAL ACCION MAYOR{:
                    SitioWeb enviarSitio = new SitioWeb();
                    enviarSitio.setIdSitioSinCorchetes(nuevoSitio.getIdSitio());
                    enviarSitio.setUsuarioCreacionSinCorchetes(nuevoSitio.getUsuarioCreacion());
                    enviarSitio.setFechaCreacionSinCorchetes(nuevoSitio.getFechaCreacion());
                    enviarSitio.setFechaModificacionSinCorchetes(nuevoSitio.getFechaModificacion());
                    enviarSitio.setUsuarioModificacionSinCorchetes(nuevoSitio.getUsuarioModificacion());
				
				
                    listaAcciones.add(enviarSitio);
				
                    nuevoSitio = new SitioWeb();
	
			:}
            | BORRARSITIO COMILLA MAYOR parametros_borrar_sitio MENOR DIAGONAL ACCION MAYOR{:
		BorrarSitioWeb enviarBorrarSitio = new BorrarSitioWeb(nuevoSitio.getIdSitio());
		listaAcciones.add(enviarBorrarSitio);
				
		nuevoSitio = new SitioWeb();
				
		:}
            | NUEVAPAGINA COMILLA MAYOR parametros_nueva_pagina etiquetas MENOR DIAGONAL ACCION MAYOR{:
		PaginaWeb enviarPagina = new PaginaWeb();
		enviarPagina.setIdPaginaSinCorchetes(nuevaPagina.getIdPagina());
		enviarPagina.setTituloSinCorchetes(nuevaPagina.getTitulo());
		enviarPagina.setSitioSinCorchetes(nuevaPagina.getSitio());
		enviarPagina.setIdPadreCorchetes(nuevaPagina.getIdPadre());
		enviarPagina.setUsuarioCreacionSinCorchetes(nuevaPagina.getUsuarioCreacion());
		enviarPagina.setFechaCreacionSinCorchetes(nuevaPagina.getFechaCreacion());
		enviarPagina.setFechaModificacionSinCorchetes(nuevaPagina.getFechaModificacion());
		enviarPagina.setUsuarioModificacionSinCorchetes(nuevaPagina.getUsuarioModificacion());
				
		List<String> listaEtiquetas = new ArrayList<>(nuevaPagina.getEtiquetas());
		enviarPagina.setEtiquetas(listaEtiquetas);
		listaAcciones.add(enviarPagina);
				
		nuevaPagina = new PaginaWeb();
			
		:}
            | BORRARPAGINA COMILLA MAYOR parametros_borrar_pagina MENOR DIAGONAL ACCION MAYOR{:
		BorrarPaginaWeb enviarBorrarPagina = new BorrarPaginaWeb(nuevaPagina.getIdPagina());
		listaAcciones.add(enviarBorrarPagina);
				
		nuevaPagina = new PaginaWeb();
				
		:}
            | MODIFICARPAGINA COMILLA MAYOR parametros_modificar_pagina etiquetas MENOR DIAGONAL ACCION MAYOR{:
		ModificarPagina enviarPaginaMod = new ModificarPagina();
		enviarPaginaMod.setIdPaginaSinCorchetes(nuevaPagina.getIdPagina());
		enviarPaginaMod.setTituloSinCorchetes(nuevaPagina.getTitulo());
		enviarPaginaMod.setSitioSinCorchetes(nuevaPagina.getSitio());
		enviarPaginaMod.setIdPadreCorchetes(nuevaPagina.getIdPadre());
		enviarPaginaMod.setUsuarioCreacionSinCorchetes(nuevaPagina.getUsuarioCreacion());
		enviarPaginaMod.setFechaCreacionSinCorchetes(nuevaPagina.getFechaCreacion());
		enviarPaginaMod.setFechaModificacionSinCorchetes(nuevaPagina.getFechaModificacion());
		enviarPaginaMod.setUsuarioModificacionSinCorchetes(nuevaPagina.getUsuarioModificacion());
				
		List<String> listaEtiquetas = new ArrayList<>(nuevaPagina.getEtiquetas());
		enviarPaginaMod.setEtiquetas(listaEtiquetas);
		listaAcciones.add(enviarPaginaMod);
				
		nuevaPagina = new PaginaWeb();
			
		:}

            | AGREGARCOMPONENTE COMILLA MAYOR parametros_agregar_componente atributos MENOR DIAGONAL ACCION MAYOR{:
		Componente enviarComponente = new Componente();
		enviarComponente.setIdComponenteSinCorchetes(nuevoComponente.getIdComponente());
		enviarComponente.setIdPaginaSinCorchetes(nuevoComponente.getIdPagina());
		enviarComponente.setClaseComponente(nuevoComponente.getClaseComponente());
		List<Atributo> listaAtributos =  new ArrayList<>(nuevoComponente.getListaAtributos());
                enviarComponente.setListaAtributos(listaAtributos);
		listaAcciones.add(enviarComponente);

                nuevoComponente = new Componente();
		:}
            | ELIMINARCOMPONENTE COMILLA MAYOR parametros_eliminar_componente MENOR DIAGONAL ACCION MAYOR{:
		EliminarComponente componenteElim = new EliminarComponente();
		componenteElim.setIdComponenteSinCorchetes(elimComponente.getIdComponente());
		componenteElim.setIdPaginaSinCorchetes(elimComponente.getIdPagina());
		listaAcciones.add(componenteElim);

                elimComponente = new EliminarComponente();
		:}
            | MODIFICARCOMPONENTE COMILLA MAYOR parametros_modificar_componente atributos MENOR DIAGONAL ACCION MAYOR{:
		ModificarComponentes modComponente = new ModificarComponentes();
			
		modComponente.setIdComponenteSinCorchetes(nuevoComponente.getIdComponente());
		modComponente.setIdPaginaSinCorchetes(nuevoComponente.getIdPagina());
		modComponente.setClaseComponente(nuevoComponente.getClaseComponente());
		List<Atributo> listaAtributos =  new ArrayList<>(nuevoComponente.getListaAtributos());
                modComponente.setListaAtributos(listaAtributos);
		listaAcciones.add(modComponente);

                nuevoComponente = new Componente();
		:}
	;


/*PARAMETROS PARA CREAR SITIOS WEB*/

parametros_crear_sitio ::= MENOR PARAMETROS MAYOR parametro_lista_crear_sitio PARAMETROSCIERRE ;

parametro_lista_crear_sitio ::= parametro_crear_sitio parametro_lista_crear_sitio
			|  parametro_crear_sitio 
			;
							
parametro_crear_sitio ::= MENOR PARAMETRO NOMBRE IGUAL  COMILLA tipo_param_crear_sitio ;

tipo_param_crear_sitio ::= parametro_tipo_id:pi {:
				nuevoSitio.setIdSitio(pi);
			:}
			| parametro_tipo_usuario_creacion:uc {:
				nuevoSitio.setUsuarioCreacion(uc);
			:}
			| parametro_tipo_fecha_creacion:fc {:
				nuevoSitio.setFechaCreacion(fc);
			:}
			| parametro_tipo_fecha_mod:fm {:
				nuevoSitio.setFechaModificacion(fm);
			:}
			| parametro_tipo_usuario_mod:um  {:
				nuevoSitio.setUsuarioModificacion(um);
			:}
			;
			
/*PARAMETROS PARA BORRAR SITIOS WEB*/

parametros_borrar_sitio ::= MENOR PARAMETROS MAYOR parametro_borrar_sitio PARAMETROSCIERRE;

parametro_borrar_sitio ::= MENOR PARAMETRO NOMBRE IGUAL  COMILLA parametro_tipo_id:pi {:
                        nuevoSitio.setIdSitio(pi);
                        :};

/*PARAMETROS PARA CREAR UNA NUEVA PAGINA*/

parametros_nueva_pagina ::= MENOR PARAMETROS MAYOR parametro_lista_nueva_pagina PARAMETROSCIERRE;

parametro_lista_nueva_pagina ::= parametro_nueva_pagina parametro_lista_nueva_pagina
				|  parametro_nueva_pagina;
							
parametro_nueva_pagina ::= MENOR PARAMETRO NOMBRE IGUAL  COMILLA tipo_param_nueva_pagina;						
							
tipo_param_nueva_pagina ::= parametro_tipo_id:pi {:
				nuevaPagina.setIdPagina(pi);
			:}
			| TITULO COMILLA MAYOR  LITERAL:li  MENOR DIAGONAL PARAMETRO MAYOR {:
				nuevaPagina.setTitulo(li);
			:}
			| SITIO COMILLA MAYOR  VALORID:id  MENOR DIAGONAL PARAMETRO MAYOR {:
				nuevaPagina.setSitio(id);
			:}
			| PADRE COMILLA MAYOR  VALORID:pa  MENOR DIAGONAL PARAMETRO MAYOR {:
				nuevaPagina.setIdPadre(pa);
			:}
			| parametro_tipo_usuario_creacion:uc {:
				nuevaPagina.setUsuarioCreacion(uc);
			:}
			| parametro_tipo_fecha_creacion:fc {:
				nuevaPagina.setFechaCreacion(fc);
			:}
			| parametro_tipo_fecha_mod:fm {:
				nuevaPagina.setFechaModificacion(fm);
			:}
			| parametro_tipo_usuario_mod:um  {:
				nuevaPagina.setUsuarioModificacion(um);
			:}
			;

/*PARAMETROS PARA ELIMINAR PAGINA*/

parametros_borrar_pagina ::= MENOR PARAMETROS MAYOR parametro_borrar_pagina PARAMETROSCIERRE;

parametro_borrar_pagina ::= MENOR PARAMETRO NOMBRE IGUAL  COMILLA parametro_tipo_id:pi {:
	nuevaPagina.setIdPagina(pi);
:};
					

/*PARAMETROS MODIFICAR PAGINA*/

parametros_modificar_pagina ::= MENOR PARAMETROS MAYOR parametro_lista_modificar_pagina PARAMETROSCIERRE;
						
parametro_lista_modificar_pagina ::= parametro_modificar_pagina parametro_lista_modificar_pagina
				|  parametro_modificar_pagina 
				;

parametro_modificar_pagina ::= MENOR PARAMETRO NOMBRE IGUAL  COMILLA tipo_param_modificar_pagina ;

tipo_param_modificar_pagina ::= parametro_tipo_id:pi {:
				nuevaPagina.setIdPagina(pi);
			:}
			| TITULO COMILLA MAYOR  LITERAL:li  MENOR DIAGONAL PARAMETRO MAYOR {:
				nuevaPagina.setTitulo(li);
			:}
			| SITIO COMILLA MAYOR  VALORID:id  MENOR DIAGONAL PARAMETRO MAYOR {:
				nuevaPagina.setSitio(id);
			:}
			| PADRE COMILLA MAYOR  VALORID:pa  MENOR DIAGONAL PARAMETRO MAYOR {:
				nuevaPagina.setIdPadre(pa);
			:}
			| parametro_tipo_usuario_creacion:uc {:
				nuevaPagina.setUsuarioCreacion(uc);
			:}
			| parametro_tipo_fecha_creacion:fc {:
				nuevaPagina.setFechaCreacion(fc);
			:}
			| parametro_tipo_fecha_mod:fm {:
				nuevaPagina.setFechaModificacion(fm);
			:}
			| parametro_tipo_usuario_mod:um  {:
				nuevaPagina.setUsuarioModificacion(um);
			:}
			;					
					
/*PARAMETROS PARA AGREGAR COMPONENTES*/

parametros_agregar_componente ::= MENOR PARAMETROS MAYOR parametro_lista_agregar_co PARAMETROSCIERRE ;



/*PARAMETROS PARA ELIMINAR COMPONENTES*/
parametros_eliminar_componente ::= MENOR PARAMETROS MAYOR parametro_lista_eliminar_co  PARAMETROSCIERRE;

parametro_lista_eliminar_co ::= parametro_eliminar_co parametro_lista_eliminar_co
				|  parametro_eliminar_co
				;
							
		
parametro_eliminar_co ::= MENOR PARAMETRO NOMBRE IGUAL  COMILLA tipo_param_eliminar_co;

tipo_param_eliminar_co ::= parametro_tipo_id:ic {:
                            elimComponente.setIdComponente(ic);
                            :}
			| PAGINA COMILLA MAYOR  VALORID:pi  MENOR DIAGONAL PARAMETRO MAYOR {:
                            elimComponente.setIdPagina(pi);
                            :}
			;

/*PARAMETROS PARA MODIFICAR COMPONENTES*/
parametros_modificar_componente ::= MENOR PARAMETROS MAYOR parametro_lista_agregar_co PARAMETROSCIERRE;

/*ESTO SIRVE PARA AGREGAR COMPONENTES Y MODIFICAR*/
parametro_lista_agregar_co ::= parametro_agregar_co parametro_lista_agregar_co
				|  parametro_agregar_co 
				;

parametro_agregar_co ::= MENOR PARAMETRO NOMBRE IGUAL  COMILLA tipo_param_agregar_co;

tipo_param_agregar_co ::= parametro_tipo_id:ic{:
				nuevoComponente.setIdComponente(ic);
			:}
			| PAGINA COMILLA MAYOR  VALORID:pi  MENOR DIAGONAL PARAMETRO MAYOR {:
				nuevoComponente.setIdPagina(pi);
			:}
			| CLASE COMILLA MAYOR  tipo_clase:tc  MENOR DIAGONAL PARAMETRO MAYOR {:
				nuevoComponente.setClaseComponente(tc);
			:}
			;
tipo_clase ::= TITULOC {:RESULT = "TITULO"; :}
	| PARRAFO {:RESULT = "PARRAFO"; :}
	| IMAGEN {:RESULT = "IMAGEN"; :}
	| VIDEO {:RESULT = "VIDEO"; :}
	| MENU {:RESULT = "MENU"; :}
	;


/*GENERALIDADES REPETIDAS*/
parametro_tipo_id ::= IDPARAM COMILLA MAYOR  VALORID:i  MENOR DIAGONAL PARAMETRO MAYOR {:RESULT = i; :};

parametro_tipo_usuario_creacion ::= USUARIOCREACION COMILLA MAYOR  VALORID:c  MENOR DIAGONAL PARAMETRO MAYOR {:RESULT = c; :};

parametro_tipo_fecha_creacion ::= FECHACREACION COMILLA MAYOR  VALORFECHA:f  MENOR DIAGONAL PARAMETRO MAYOR  {:RESULT = f; :};

parametro_tipo_fecha_mod ::= FECHAMODIFICACION COMILLA MAYOR  VALORFECHA:m  MENOR DIAGONAL PARAMETRO MAYOR {:RESULT = m; :};

parametro_tipo_usuario_mod ::= USUARIOMODIFICACION COMILLA MAYOR  VALORID:u  MENOR DIAGONAL PARAMETRO MAYOR {:RESULT = u; :};

etiquetas ::= CONTENIDOETIQUETA etiqueta  ETIQUETASCIERRE
            |empty
	;
etiqueta ::= MENOR ETIQUETA VALORETIQUETA IGUAL COMILLA DATOETIQUETA:et1 COMILLA DIAGONAL MAYOR etiqueta {:
		nuevaPagina.setEtiqueta(et1);
	:}
	|  MENOR ETIQUETA VALORETIQUETA IGUAL COMILLA DATOETIQUETA:et2 COMILLA DIAGONAL MAYOR {:
		nuevaPagina.setEtiqueta(et2);
	:}
	;

		
atributos ::= MENOR ATRIBUTOS MAYOR atributo_lista  ATRIBUTOSCIERRE
	;
			
atributo_lista ::= atributo atributo_lista
		|  atributo 
		;
atributo ::= MENOR ATRIBUTO NOMBRE IGUAL COMILLA valor_atributo:va COMILLA MAYOR LITERAL:li MENOR DIAGONAL ATRIBUTO MAYOR {:
	nuevoComponente.setAtributo(new Atributo(va, li));
:}
			;
valor_atributo ::= TEXTO {:RESULT = "TEXTO"; :}
		| ALINEACION {:RESULT = "ALINEACION"; :}
		| COLOR {:RESULT = "COLOR"; :}
		| ORIGEN {:RESULT = "ORIGEN"; :}
		| ALTURA {:RESULT = "ALTURA"; :}
		| ANCHO {:RESULT = "ANCHO"; :}
		| PADRE {:RESULT = "PADRE"; :}
		| ETIQUETAS {:RESULT = "ETIQUETAS"; :}
                ;
			
			
empty ::= /* empty */ ;